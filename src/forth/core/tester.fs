\ Inspired by http://lars.nocrew.org/forth2012/testsuite.html
\ and simplified for SimForth
\
\ This file creates words for unit testing Forth words

DECIMAL

VARIABLE ACTUAL-DEPTH           \ STACK RECORD
CREATE   ACTUAL-RESULTS 32 CELLS ALLOT
VARIABLE START-DEPTH

: EMPTY-STACK           \ ( ... -- ) EMPTY STACK: HANDLES UNDERFLOWED STACK TOO.
    DEPTH START-DEPTH @ < IF
        DEPTH START-DEPTH @ SWAP DO 0 LOOP
    THEN
    DEPTH START-DEPTH @ > IF
        DEPTH START-DEPTH @ DO DROP LOOP
    THEN
;

: DISP-LINE ( TYPE SOURCE TYPE ) . CR ;
: ERROR DISP-LINE EMPTY-STACK ;
: CORRECT-RES ( S" OK RESULT: " ) 1 DISP-LINE ;
: INCORRECT-RES ( S" INCORRECT RESULT: " ) 2 ERROR ;
: WRONG-NUMBER ( S" WRONG NUMBER OF RESULTS: " ) 3 ERROR ;

: T{        \ ( -- ) SYNTACTIC SUGAR.
    DEPTH START-DEPTH !
;

: ->        \ ( ... -- ) RECORD DEPTH AND CONTENT OF STACK.
    DEPTH DUP ACTUAL-DEPTH !        \ RECORD DEPTH
    START-DEPTH @ > IF              \ IF THERE IS SOMETHING ON STACK
        DEPTH START-DEPTH @ - 0 DO ACTUAL-RESULTS I CELLS + ! LOOP \ SAVE THEM
    THEN
;

: }T        \ ( ... -- ) COMPARE STACK (EXPECTED) CONTENTS WITH SAVED (ACTUAL) CONTENTS.
    DEPTH ACTUAL-DEPTH @ = IF        \ IF DEPTHS MATCH
        DEPTH START-DEPTH @ > IF         \ IF THERE IS SOMETHING ON THE STACK
            DEPTH START-DEPTH @ - 0 DO               \ FOR EACH STACK ITEM
                ACTUAL-RESULTS I CELLS + @  \ COMPARE ACTUAL WITH EXPECTED
                <> IF INCORRECT-RES LEAVE ELSE CORRECT-RES THEN
            LOOP
        THEN
    ELSE                 \ DEPTH MISMATCH
        WRONG-NUMBER
    THEN
;
